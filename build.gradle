buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-release" }
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.1.4.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'

jar {
    baseName = 'cuenation-api'
    version = '0.1.0'
}

springBoot {
    mainClass = "cuenation.api.Application"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.spring.io/libs-release" }
    maven { url "http://m2.neo4j.org" }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.name == 'log4j') {
            details.useTarget "org.slf4j:log4j-over-slf4j:1.7.7"
        }
    }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("com.fasterxml.jackson.core:jackson-databind")
    compile("org.springframework.hateoas:spring-hateoas")
    compile("org.springframework.plugin:spring-plugin-core:1.1.0.RELEASE")
    compile("com.jayway.jsonpath:json-path:0.9.1")
    compile("org.springframework.boot:spring-boot-starter-data-mongodb")
    compile 'rome:rome-fetcher:1.0'
    compile 'rome:rome:1.0'
    compile 'org.jsoup:jsoup:1.7.+'

    //common dependencies
    compile "org.slf4j:slf4j-api:1.7.7"
    compile 'log4j:log4j:1.2.17'

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.springframework:spring-test")
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.mockito:mockito-all:1.+'
}

task updateCueCategories(type: JavaExec) {
    description = 'Updates cue categories from "http://cuenation.com/?page=categories" page'
    group = 'Cli'
    main = 'cuenation.api.cli.UpdateCueCategories'
    classpath = sourceSets.main.runtimeClasspath
}
task updateCues(type: JavaExec) {
    description = 'Updates cues from "http://cuenation.com/feed.php" page'
    group = 'Cli'
    main = 'cuenation.api.cli.UpdateCues'
    classpath = sourceSets.main.runtimeClasspath
}
